plugins {
    id 'groovy'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version '0.21.0'
    id 'maven-publish'
}

group 'com.appunite.placeholdersvalidator'
version '1.0.2'

repositories {
    jcenter()
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.7.0"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

gradlePlugin {
    plugins {
        placeholdersValidator {
            id = 'com.appunite.placeholdersvalidator'
            implementationClass = 'com.appunite.placeholdersvalidator.PlaceholdersValidatorPlugin'
        }
    }
}

pluginBundle {
    description = 'Gradle plugin which validates placeholders from translated strings.xml files by comparing them with main strings.xml file.'
    website = 'https://github.com/appunite/android-placeholders-validator'
    vcsUrl = 'https://github.com/appunite/android-placeholders-validator'
    tags = ['android', 'strings', 'translations', 'resources', 'localization', 'placeholders', 'validation']

    plugins {
        placeholdersValidator {
            displayName = 'Placeholders Validator'
        }
    }
}

// For publishing to maven local. Use command ./gradlew publishToMavenLocal
publishing {
    publications {
        pluginPublication(MavenPublication) {
            from components.java
            groupId project.group
            version project.version
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// Workaround for https://github.com/gradle/gradle/issues/17236
tasks.withType(Copy).all {
    duplicatesStrategy 'exclude'
}
